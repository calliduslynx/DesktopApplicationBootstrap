// ***********************************
// ***** kotlin build part
// ***********************************
buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()                  // <-- required by kotlin
        jcenter()                       // <-- required by launch4j
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'edu.sc.seis.gradle:launch4j:2.4.4'
    }
}

// ***********************************
// ***** plugins
// ***********************************
apply plugin: "kotlin"
apply plugin: "java"
apply plugin: "application"             // <-- provides "run" - task
apply plugin: "edu.sc.seis.launch4j"    // <-- https://github.com/TheBoegl/gradle-launch4j

// ***********************************
// ***** parameters
// ***********************************
group = "de.mabe.catstore"

mainClassName = "de.mabe.catstore.MainKt"

// ***********************************
// ***** configuration
// ***********************************
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

launch4j {
    mainClassName = "de.mabe.catstore.MainKt"
    icon = "${projectDir}/src/main/resources/catstore.ico"
}

compileKotlin {
    kotlinOptions {
        apiVersion = "1.3"
        languageVersion = "1.3"
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        apiVersion = "1.3"
        languageVersion = "1.3"
        jvmTarget = "1.8"
    }
}

tasks.withType(Test) {
    testLogging {
        events /*'started',*/ 'passed', 'failed'
        exceptionFormat = 'full'
        afterSuite { desc, result -> if (!desc.parent) println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)" }
    }
}

// ***********************************
// ***** dependencies
// ***********************************
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.11"                        // Kotlin support
    compile "org.jetbrains.kotlin:kotlin-reflect:1.2.50"

    compile "org.slf4j:log4j-over-slf4j:1.7.21"                                     // Logs
    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'org.openjfx:javafx-controls:12-ea+4'

    testCompile 'junit:junit:4.12'
}